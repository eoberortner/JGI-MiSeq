#!/usr/bin/env perl
use Modern::Perl;
use Getopt::Long;
use File::Basename;
use List::MoreUtils qw/uniq/;
use Cwd;
use YAML qw/LoadFile/;
use GT::Config;
use GT::Clutils;
use GT::JGI;

load_modules_module( 'picard' );

my $NOMOD_ENV = '/global/dna/projectdirs/RD/reseq_store/opt/etc/env.reseq.sh';

umask 2;

my ( $help, $align_dir, $sge_project, $ref_file, $aln_args, $sge_args, $samXe_args, $bwa_module,
	   $modules, $no_db_for_rg, $bwa_algorithm, $verbose, $do_analysis_task_stuff, $DEBUG );

$DEBUG         = 0;
$modules       = 0;
$align_dir     = 'bwa_dir';
$bwa_algorithm = 'mem';

$do_analysis_task_stuff = 0;

my $lib_file = '';

GetOptions(
	'basedir=s'                => \$align_dir,
	'ref=s'                    => \$ref_file,
	'config_file|libfile=s'    => \$lib_file,
	'Project=s'                => \$sge_project,
	'verbose'                  => \$verbose,
  'aln-args=s'               => \$aln_args,
	'sampe-args=s'             => \$samXe_args,
	'sge-args=s'               => \$sge_args,
	'module-bwa|module_bwa=s'  => \$bwa_module,
	'algo-bwa=s'               => \$bwa_algorithm,
	'modules!'                 => \$modules,
	'at'                       => \$do_analysis_task_stuff,
  'weird_fq_name'            => \$no_db_for_rg,
	'DEBUG'                    => \$DEBUG,
	'help'                     => \$help,
);
#print "modules = $modules\n"; die;
sub usage {
	my $bin = basename $0;
	print STDERR <<EOH;

$bin [options] -c libs.info

e.g. $bin -algo backtrack -modules -c libs.inf  \# backtrack (aln + sampe/samse) algorithm using the 'modules' version of bwa
     $bin -c libs.info                          \# mem algorithm ( best unless read length < 70  ), avoiding modules version of bwa

          *tyically run w/o options on directory prepared with setup_dirs

 -algo-bwa       STRING   mem (default), backtrack or bwasw
 -aln-args       STRING   args for bwa aln ( e.g. -aln-args \'-M 4 -O 14\' )
 -sampe-args     STRING   args for bwa sampe or samse ( e.g. -sampe-args \'-M 4 -O 14\' )
 -sge-args       STRING   args for sge ( e.g. -sge-ags \"-l high.c\" ***USE THEM QUOTES***

 -module-bwa     STRING   name of bwa module to use ( e.g. bwa/0.5.9 )
 -modules        FLAG     no demodulate ( -nomodules is default, meaning demodulate )
 -weird_fq_name           (-weird ok also) Don't parse fastq filename for RG tag information, don't lookup in database either
 -config         libinfo  If reads not in run-id.lane.* (  run-id.lane.basecall-id.barcode ) 
                          this file needed to map read names to library names and samples
                          *also used if reads aren't in venonat
 -base           DIR      name of subdirectory to run in

 -noat                    SKIP analysis task updating
 -P              STRING   sge accounting project name for qsub

 -ref            FILE.fa  if no dir/base/config.yml, reference file to align against
                          must be already be indexed by bwa
 -v                       verbose, print more and ask run_bwa to print more

EOH
}

if ( $help || ( ! @ARGV && ! ( defined $lib_file && -e $lib_file ) )
   || !defined $align_dir || ( defined $ref_file && !-e $ref_file ) ) {    # ref from config usually now
	usage();
	exit;
}

if ( $DEBUG ) { 
	$ENV{PATH} = "~j_martin/dev/jel-pipe:$ENV{PATH}";
}

my $no_mod_option = $modules ? '' : '-nomodules'; 

my $set_ref_by_config;

if ($ref_file) {
	# TODO: add updating configs, ** also, check all configs before launching anything **
	$set_ref_by_config = 0;
}
else {
	$set_ref_by_config = 1;
}

sub get_ref_from_config {
	my $config = GT::Config->new();
	$config->load();

	$ref_file = $config->{ref_fasta};
	if ( !defined $ref_file || !-e $ref_file ) {
		warn "ERROR: no ref_fasta entry in config.yml";
		usage();
		exit;
	}
	return $ref_file;
}

# $ref_file ||=  '/global/u1/j/j_martin/genomes/Populus_trichocarpa_with_chloroplast_and_mitochondria.fa';

# $lib_file ||=  'runstlist.libinfo';

#TODO: standardize qsub calls, there's this and some direct ones.
my $project_option = defined $sge_project ? "-P $sge_project " : '';
my $qs = "qsub -terse -cwd -b yes -now no -j yes -m as -M eoberortner\@lbl.gov -w e $project_option ";

my @cmds = ();
my %cmds = ();

my $start_dir = getcwd;

#
#  get RG information from a text file instead of databases
#
# lib file format is ]
# id, lib, full_path_fq, gen, sp, strain, isolate
#   names are ignored at the moment though
# switch to getting from config.yml
#  - updated by split_fq
#

my %libs     = ();
my @dir_list = @ARGV;

if ( $lib_file && -e $lib_file ) {
	my %dir_list = ();
	my $order    = 1;
	open( my $lfh, $lib_file ) or die $!;
	while (<$lfh>) {
		next if ( /^#/ || /^\s*$/ );
		my ( $pmo_info, $lib, $fp_fq ) = split;

		#TODO: should be shared function with slice_fastq
		my $fq_name = truncate_fastq_filename($fp_fq);

		if ( $DEBUG ) { print STDERR "libs{ $fq_name } = $lib\n"; }

		$libs{$fq_name} = $lib;
		$dir_list{$pmo_info} = $order++;
	}
	push @dir_list, sort { $dir_list{ $a } <=> $dir_list{ $b } } keys %dir_list;
}

sub truncate_fastq_filename {
	my ($name) = @_;
	$name = basename $name;
	$name =~ s/[.]fastq.gz$//;
	$name =~ s/[.]fq.gz$//;
	return ($name) if ($name);
}

####
# sliced look like
# pe-2096.7.1751.2@176000001.fq.gz
# pe-2096.7.1751.ABCDEF.2@176000001.fq.gz
####

sub get_lib_from_databases {
	my ( $run, $lane, $basecall, $barcode ) = @_;
	my $lib;
	if ( $run >= 6000 ) {    # ITS system
		my $fname = $barcode ? "$run.$lane.$basecall.$barcode.fastq.gz" : "$run.$lane.$basecall.fastq.gz";
		my $dw_info = `get_dw_info runs2info $fname`;
		if ( !( $dw_info =~ /^WARNING/ ) ) {
			($lib) = ( split /\t/, $dw_info )[1];
		}
	}
	elsif ($barcode) {
		my $multiplex = `get_multiplex_lib_info $run.$lane.$basecall.$barcode`;
		chomp $multiplex;
		my ( $pmoinfo, $mlib ) = split /\t/, $multiplex if ($multiplex);
		$lib = $mlib;
	}
	else {
		$lib = get_lib_from_venonat( $run, $lane );
	}
	return ($lib);
}

sub get_lib_from_venonat {
	my ( $run, $lane ) = @_;
	my $lib = `get_venal_info runlane2lib $run $lane`;
	chomp $lib;

	return ($lib);
}


my $bwa_module_option = $bwa_module ? "-module-bwa $bwa_module" : '';

for my $dir ( grep { -d } uniq @dir_list ) {
	my $pinfo = basename $dir;
	if ( !-e "$dir/$align_dir" ) {
		warn "skipping $dir -- no $align_dir\n";
	}

	# analysis task stuff
  # TODO: move code out of here
	#
	if ( $do_analysis_task_stuff ){ 
		#	~/dev/jel-pipe/analysis_task set -fdid 1063277 -type map  -state started
		my ( $id ) = ( split /_/, $pinfo )[ 1 ];
		if ( $id && $id =~ /^\d+$/ ) {
			for my $type ( qw/map snp sv/ ) {
				my $cmd = "analysis_task set -fdid $id -type $type  -state started";
				my $response = `$cmd`;
				if ( $? || ! defined $response || $response != 'success' ) {
					warn "WARNING: cmd maybe failed $cmd";
				}
			}
		}
	}

	my @bwas;
	chdir "$dir/$align_dir" or die "can't cd to $dir/$align_dir $!";

	if ($set_ref_by_config) {
		$ref_file = get_ref_from_config;
	}

	# load config for read info ( 
	# *** combine with above?

	my $config = LoadFile( 'config.yml' );
	for my $cfg_fq ( keys %{ $config->{fqs } } ) {
		my $trunc_fq_name = truncate_fastq_filename( $cfg_fq );
		if ( ! exists $libs{ $trunc_fq_name } ) {
			print STDERR "uh adding $trunc_fq_name for $cfg_fq = $config->{fqs}{$cfg_fq}->{lib}:\n";
			$libs{ $trunc_fq_name } = $config->{fqs}{$cfg_fq}->{library};
		}
	}
	
	if ( !-e "bam_dir" ) {
		mkdir 'bam_dir' or die "ERROR: failed to create $dir/bam_dir $!";
	}

	my ( @peRGs, @peR1s, @peR2s );  # ReadGroups, Read1s, Read2s

	my %run_lanes = ();

	#
	# get information for paired reads
	#
	for my $read1 ( glob("fastq_dir/pe-*.1@[0-9]*.fq.gz") ) {
		my $read2 = $read1;

		# change .1@ to .2@  - a bit excessive on the exact matching, sorry if it makes it look messy.

		$read2 =~ s/[.]1\@(\d+)[.]fq[.]gz$/.2\@$1.fq.gz/;

		if ( !-e $read1 or !-e $read2 ) { die "ERROR: can't find  $read1 or maybe $read2 in $dir" }

		my ( $id, $lib );
		if ( $no_db_for_rg ) { 
			( $id ) = split /\@/, basename $read1;
			$id =~ s/^pe-//;
			$id =~ s/[.]1$//;
			if ( ! exists $libs{ $id } ) {
				die "no lib for $id ( from $read1 ) in $dir";
			}
			$lib = $libs{ $id };
		}
		else {
			my ( $run, $lane, $bc, $barcode, $end, $seg ) = parse_read( $read1 );
		
			die "ERROR: can't parse $read1 in $dir" unless ( defined $run && defined $lane && defined $bc );

			my ($read_basename) = split /\@/, basename $read1;    # for filename matching from libs.info file
			$read_basename =~ s/^pe-//;                           # remove pairing indicator
			$read_basename =~ s/[.]1$//;                          # remove read1/2 indicator

			if( $DEBUG ) { print STDERR  "check for libs{ $read_basename }\n"; }

			$lib = exists $libs{$read_basename} ? $libs{$read_basename} : get_lib_from_databases( $run, $lane, $bc, $barcode );

			die "no lib for $read1($read_basename) in $dir" unless ( defined $lib );

			$id = exists $libs{$read_basename} ? $read_basename
		      : $barcode                     ? "$run.$lane.$bc.$barcode"
		      :                                "$run.$lane.$bc";
		}

		my $rg = "\'\@RG\\tID:$id\\tSM:$pinfo\\tPL:illumina\\tLB:$lib\'";

		push @peRGs, $rg;
		push @peR1s, $read1;
		push @peR2s, $read2;

	}

	#
	# get information for single ended reads
	#
	my ( @seRGs, @seR1s );

	for my $read1 ( glob("fastq_dir/se-*") ) {
		if ( !-e $read1 ) { die "fail on $read1 in $dir" }

		my ( $id, $lib );
		if ( $no_db_for_rg ) { 
			( $id ) = split /\@/, basename $read1;
			$id =~ s/^se-//;
			if ( ! exists $libs{ $id } ) {
				die "no lib for $id ( from $read1 ) in $dir";
			}
			$lib = $libs{ $id };
		}
		else {

			my ( $run, $lane, $bc, $barcode, $end, $seg ) = parse_read( $read1 );
			die "can't parse $read1" unless ( defined $run && defined $lane && defined $bc );

			my ($read_basename) = split /\@/, basename $read1;    # for filename matching from libs.info file
			$read_basename =~ s/^se-//;                           # remove pairing indicator
			$read_basename =~ s/[.]1$//;                          # remove read1/2 indicator

			if( $DEBUG ) { print STDERR  "check for libs{ $read_basename }\n"; }
	
			$lib = exists $libs{$read_basename} ? $libs{$read_basename} : get_lib_from_databases( $run, $lane, $bc, $barcode );
	
			die "no lib for $read1 in $dir" unless ( defined $lib );
	
			$id = exists $libs{$read_basename} ? $read_basename
			    : $barcode                     ? "$run.$lane.$bc.$barcode"
			    :                                "$run.$lane.$bc";
		}
		my $rg = " \'\@RG\\tID:$id\\tSM:$pinfo\\tPL:illumina\\tLB:$lib\'";

		push @seRGs, $rg;
		push @seR1s, $read1;

	}

	my $mixed        = @seRGs && @peRGs  ? 1 : 0;
	my $sge_args_opt = defined $sge_args ? "-sge-args \'$sge_args\'" : '';
	my @hold_jids    = ();

	if (@seRGs) {
		my $out = $mixed ? "$pinfo.single-end" : $pinfo;
		my $cmd = "run_bwa -algo $bwa_algorithm $no_mod_option $bwa_module_option $sge_args_opt -ref  $ref_file -read1 " . join( " ", @seR1s ) . " -rg " . join( " ", @seRGs ) . " ";
		$cmd .= " $project_option -out $out -verbose -thread 4 -mem 8";
		print STDERR "$pinfo $cmd\n";
		my $rval = `$cmd`;
		push @hold_jids, split /\s+/, $rval;
	}
	if (@peRGs) {
		my $out = $mixed ? "$pinfo.paired-end" : $pinfo;
		my $cmd = "run_bwa -algo $bwa_algorithm $no_mod_option $bwa_module_option $sge_args_opt  -ref  $ref_file -read1 " . join( " ", @peR1s ) . " -read2 " . join( " ", @peR2s ) . " -rg " . join( " ", @peRGs ) . " ";
		$cmd .= " $project_option -out $out -verbose -thread 4 -mem 8";
		print STDERR "$pinfo $cmd\n";
		my $rval = `$cmd`;
		push @hold_jids, split /\s+/, $rval;
	}
	if ($mixed) {    # mixture :(
		my @cmds = ();
		if ( ! $modules ) { 
			push @cmds, "source $NOMOD_ENV";
		}	
		else {
			push @cmds, "module load picard";
		}

		my $jids = join( ",", @hold_jids );
		my $cmd = "picard -m 11 MergeSamFiles I=$pinfo.single-end.bam I=$pinfo.paired-end.bam O=$pinfo.bam ";
		$cmd .= "AS=true VALIDATION_STRINGENCY=SILENT MAX_RECORDS_IN_RAM=8000000 CREATE_INDEX=true ";
		$cmd .= "SO=coordinate CO=FixMateInformation CO=MarkDuplicates";

		push @cmds, $cmd;
		push @cmds, "mv $pinfo.bai $pinfo.bam.bai";
		push @cmds, "rm $pinfo.single-end.bam $pinfo.single-end.bam.bai";
		push @cmds, "rm $pinfo.paired-end.bam $pinfo.paired-end.bam.bai";
		push @cmds, "samtools flagstat $pinfo.bam > $pinfo.bam.flagstat";

		write_array_cmds( \@cmds, 0, "run_remerger_${$}" );

		my $qsub = "$qs -l normal.c,ram.c=13G,h_vmem=13G,h_rt=10:45:45 -hold_jid $jids -N remerger.$jids ./run_remerger_${$}.0";
		print STDERR "$pinfo $qsub\n";
		my ($rjid) = split /\s+/, `$qsub`;
		print "$rjid\n";
	}
	else {
		print join(",", @hold_jids), "\n";
	}
	print "\n";
	chdir $start_dir or die "can't cd back to $start_dir $!";
	sleep 5;
}
sub parse_read {
	my ( $read ) = @_;
	my @read = split /[.@]/, $read;
	my $parts = scalar @read;
	#( $read[ 0 ] ) = ( split /\//, $read[ 0 ] )[ 1 ];
	$read[ 0 ] = basename $read[0];
	my $is_paired = $read[ 0 ] =~ /^pe-/ ? 1 : 0;
	$read[ 0 ] =~ s/^[ps]e-//;
	# 12 is to stick an undef into barcode
	my ( $run, $lane, $basecall, $barcode, $end, $seg );
	if ( $is_paired ) {
		( $run, $lane, $basecall, $barcode, $end, $seg ) = $parts == 7 ? @read[ 0, 1, 2, 12, 3, 4 ]
                                                     : $parts == 8 ? @read[ 0, 1, 2, 3, 4, 5 ]
                                                     : $parts == 9 ? @read[ 0, 1, 2, 4, 5, 6 ]
                                                     : do { die "don't understand $read, split into @read" };
	}
	else {
		( $run, $lane, $basecall, $barcode, $end, $seg ) = $parts == 6 ? @read[ 0, 1, 2, 12, 12, 3 ]
                                                     : $parts == 7 ? @read[ 0, 1, 2, 3, 12, 4 ]
                                                     : $parts == 8 ? @read[ 0, 1, 2, 4, 12, 5 ]
                                                     : do { die "don't understand $read, split into @read" };
	}
	return(  $run, $lane, $basecall, $barcode, $end, $seg );
}

